---
title: Electron NSIS 读取注册表判断版本号
date: 2023-10-15 00:57:17
permalink: /pages/b50eb6/
categories:
  - 技术
  - Electron
tags:
  - Electron
  - NSIS
author: 
  name: huanggaoqing
  link: https://github.com/huanggaoqing
---
## nsis脚本
```bash
!include LogicLib.nsh
!include WordFunc.nsh
!define PRODUCT_VERSION "0.7.0"
!define PRODUCT_PUBLISHER "My company, Inc."
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_INSTALL_KEY "Software\Microsoft\Windows\CurrentVersion\Install\${PRODUCT_NAME}"
RequestExecutionLevel admin

!macro customInit
  Call InitMethods
!macroend

; 内置两种判断方式
; 取决是否想在待安装版本高的情况下也弹出覆盖安装的提示
Function InitMethods
  ; 读取注册表版本号
  ReadRegStr $0 HKCU "${PRODUCT_INSTALL_KEY}" "Version"
  ; 判断版本号是否为空
  StrCmp $0 "" isEmpty
    ; 对比版本号 
    ; 0 相等 1 待安装的大 2 已经安装的大
    ${VersionCompare} "${PRODUCT_VERSION}" $0 $R2
    ; 为 2 提示不用安装
    ${If} $R2 == 2 
      MessageBox MB_ICONSTOP "安装程序检测到 ${PRODUCT_NAME} $0 已经安装了更新版本"
      Quit
      Goto done
    ; 为 0 提示覆盖安装
    ${ElseIf} $R2 == 0 
      MessageBox MB_YESNO  "安装程序检测到 ${PRODUCT_NAME} $0 已经安装，是否覆盖安装" IDYES Y IDNO N
      N: 
        Quit
        Goto done
      Y:
        ; 覆盖安装时设置删除标识为 N
        WriteRegStr HKCU  "${PRODUCT_INSTALL_KEY}" "IS_DELETE" "N"
        Goto done
    ; 其他 case 直接安装
    ${Else} 
      ; 覆盖安装时设置删除标识为 N
      WriteRegStr HKCU  "${PRODUCT_INSTALL_KEY}" "IS_DELETE" "N"
      Goto done
    ${EndIf}

    ; IntCmp $R2 2 numbersAreEqual numbersAreNotEqual
    ; ; 为 2 就提示不用安装
    ; numbersAreEqual:
    ;   MessageBox MB_ICONSTOP "安装程序检测到 ${PRODUCT_NAME} $0 已经安装了更新版本"
    ;   Quit
    ;   Goto done
    ; ; 其他case提示覆盖安装
    ; numbersAreNotEqual:
    ;   MessageBox MB_YESNO  "安装程序检测到 ${PRODUCT_NAME} $0 已经安装，是否覆盖安装" IDYES Y IDNO N
    ;   N: 
    ;     Quit
    ;     Goto done
    ;   Y:
    ;     Goto done
  isEmpty:
    DetailPrint "version empty"
  done:
FunctionEnd

; 删除数据库文件
Function un.DeleteDataBase
  ; 拼接文件地址
  StrCpy $0 "$PROFILE\xender"
  StrCpy $1 "database.db"
  StrCpy $2 "$0\$1"

  ; 删除文件
  Delete $2
FunctionEnd

; 更新时设置删除标识为 N
Section "Update"
  WriteRegStr HKCU "${PRODUCT_INSTALL_KEY}" "IS_DELETE" "N"
SectionEnd

!macro customUnInstall 
  ; 读取标识判断是否删除
  ReadRegStr $0 HKCU "${PRODUCT_INSTALL_KEY}" "IS_DELETE"
  ${If} $0 != "N"  
    Call un.DeleteDataBase
  ${EndIf}
  DeleteRegKey HKCU  "${PRODUCT_INSTALL_KEY}"
  ReadRegStr $0 HKCU  "${PRODUCT_INSTALL_KEY}" ""
  DeleteRegKey HKCU  "$0"
  DeleteRegKey HKCU  "${PRODUCT_NAME}"
  ReadRegStr $0 HKCU  "${PRODUCT_NAME}" ""
  DeleteRegKey HKCU  "$0"
!macroend

!macro customInstall
  WriteRegStr HKCU  "${PRODUCT_INSTALL_KEY}" "Version" "${PRODUCT_VERSION}"   ; 根键 子键 项 值
  DetailPrint "Register ${PRODUCT_NAME} URI Handler"
  DeleteRegKey HKCU  "${PRODUCT_NAME}"
  WriteRegStr HKCU  "${PRODUCT_NAME}" "" "${PRODUCT_NAME}"
  WriteRegStr HKCU  "${PRODUCT_NAME}" "URL Protocol" ""
  WriteRegStr HKCU  "${PRODUCT_NAME}\DefaultIcon" "" "$INSTDIR\${APP_EXECUTABLE_FILENAME}"
  WriteRegStr HKCU  "${PRODUCT_NAME}\shell" "" ""
  WriteRegStr HKCU  "${PRODUCT_NAME}\shell\Open" "" ""
  WriteRegStr HKCU  "${PRODUCT_NAME}\shell\Open\command" "" "$INSTDIR\${APP_EXECUTABLE_FILENAME} %1"
!macroend
```
## 在打包配置中配置
```json
nsis: {
  // 是否一键安装，建议为 false，可以让用户点击下一步、下一步、下一步的形式安装程序，如果为true，当用户双击构建好的程序，自动安装程序并打开，即：一键安装（one-click installer）
  oneClick: false,
  // 允许请求提升。 如果为false，则用户必须使用提升的权限重新启动安装程序。
  allowElevation: true,
  // 允许修改安装目录，建议为 true，是否允许用户改变安装目录，默认是不允许
  allowToChangeInstallationDirectory: true,
  // perMachine: true,   //值为布尔类型，代表是否显示辅助安装程序的安装模式安装程序页面（选择按机器还是按用户）。true时代表始终按用户安装
  // 安装图标
  installerIcon: "build/electron-icon/icon.ico",
  // 卸载图标
  uninstallerIcon: "build/electron-icon/icon.ico",
  // 安装时头部图标
  installerHeaderIcon: "build/electron-icon/icon.ico",
  // 创建桌面图标
  createDesktopShortcut: true,
  // 创建开始菜单图标
  createStartMenuShortcut: true,
  warningsAsErrors: false ,
  include: "installer_new.nsi",
},
```
